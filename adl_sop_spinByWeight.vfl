// Spins items using an existing weight . Can be applied to either packed primitives or most standard instance attributes.
// adl_sop_spinByWeight.vfl, by Andrew Louda
// Modified: 2024-10-27 - Updated to new paramter syntax and added randomization within wrangle along and a unser-defined rotation order.
// Copyright 2024 Andrew Louda. This file is licensed under the Apache 2.0 license.
// SPDX-License-Identifier: Apache-2.0

/* 
    The core functionality of this snippet is very simple, but it has been generalized so that it can operate on many different types of attributes.
    Spin weight is expected to be a vector ranging from -1 to 1 (though it can be whatever you want), 
    with max_rotate dictating the maximum rotation in degrees.
*/


int weight_method = chi('weight_method'); // [[menu_items=('rand','weight','const'), menu_labels=('Random Per Element','Weight Attribute','Constant') ]]
float spin_weight = 1;
if(weight_method == 0){
    // folder[[name=rand_parms, folder_type='Collapsible', label='Configure Random', hide_when="{ weight_method != 0 }"]]
    string seed_attrib = chs('seed_attribute'); // [[folder=rand_parms, preset=attribSelectInt, help='Uses elemnum if blank']]
    int seed_primary = 0;
    if( seed_attrib != '' ){
        seed_primary = getattrib(0, chs('class'), seed_attrib, i@elemnum, 0);
    }
    else{
        seed_primary = i@elemnum;
    }
    float seed_secondary = chf("secondary_seed"); // [[folder=rand_parms,max=123]]
    spin_weight = rand(set( seed_primary, seed_secondary))*2-1;
}
else if(weight_method == 1){
    // folder[[name=weight_parms, folder_type='Collapsible', label='Configure Weight', hide_when="{ weight_method != 1 }"]]
    spin_weight = getattrib(0, chs('class'), chs('weight_attribute'), i@elemnum, 0); // [[folder=weight_parms, preset=attribSelectFloat, default='spinweight']]
}

int rOrd = chi('rOrd'); // [[label='Rotation Order', menu_pairs={'xyz':'Rx Ry Rz','xzy':'Rx Rz Ry','yxz':'Ry Rx Rz','yzx':'Ry Rz Rx','zxy':'Rz Rx Ry','zyx':'Rz Ry Rx'}]]
vector rotamt = radians(chv("max_rotate")) * spin_weight; // [[default=360]]
vector4 qspin = eulertoquaternion(rotamt,rOrd);

string output_type = chs('output_type'); // [[menu_pairs={'orient':'Orient','rot':'Rot','transform3':'Transform (3x3)','transform4':'Transform (4x4)','packed':'Packed Prims'} ]]

if(output_type == 'orient'){
    vector4 baseorient;
    if( hasattrib(0,chs('class'),'orient') ){
        baseorient = attrib(0,chs('class'),'orient',@elemnum); 
    }
    else{
        vector N = hasattrib(0,chs('class'),'N') ? normalize(attrib(0,chs('class'),'N',@elemnum)) : {0,1,0};
        vector up = hasattrib(0,chs('class'),'up') ? normalize(attrib(0,chs('class'),'up',@elemnum)) : {0,0,-1};
        baseorient = quaternion( chi('swap_n_up') ? maketransform( up, N) : maketransform( N, up) );  // [[is_toggle ,hide_when='{ output_type != orient }', help='If using N-up for base orient, swap which vector is used for which.' ]]
    }
    vector4 neworient = qmultiply(baseorient, qspin);
    setattrib( 0, chs('class'), 'orient', @elemnum, @vtxnum, neworient, 'set');
}

else if(output_type == 'rot'){
    vector4 newrot;
    if( hasattrib(0,chs('class'),'rot') ){
        vector4 baserot = attrib(0,chs('class'),'rot',@elemnum);
        newrot = qmultiply(baserot, qspin);
    }
    else newrot = qspin;
    setattrib( 0, chs('class'), 'rot', @elemnum, @vtxnum, newrot, 'set');
}

else if(output_type == 'transform3'){
    if( hasattrib(0,chs('class'),'transform') ){
        matrix3 xform = getattrib(0,chs('class'),'transform',@elemnum,0);
        chi('prerotate') ? prerotate(xform,rotamt,rOrd) : rotate(xform,rotamt,rOrd);  // [[is_toggle, hide_when='{ output_type == orient } { output_type == rot }']]
        setattrib( 0, chs('class'), 'transform', @elemnum, @vtxnum, xform, 'set');
    }
    else{
        matrix3 xform = qconvert(qspin);
        setattrib( 0, chs('class'), 'transform', @elemnum, @vtxnum, xform, 'set');
    }
}

else if(output_type == 'transform4'){
    if( hasattrib(0,chs('class'),'transform') ){
        matrix xform = getattrib(0,chs('class'),'transform',@elemnum,0);
        chi('prerotate') ? prerotate(xform,rotamt,rOrd) : rotate(xform,rotamt,rOrd);
        setattrib( 0, chs('class'), 'transform', @elemnum, @vtxnum, xform, 'set');
    }
    else{
        matrix xform = qconvert(qspin);
        setattrib( 0, chs('class'), 'transform', @elemnum, @vtxnum, xform, 'set');
    }
}

else if(output_type == 'packed'){
    matrix3 base_xform = primintrinsic(0,'transform',@elemnum);
    chi('prerotate') ? prerotate(base_xform,rotamt,rOrd) : rotate(base_xform,rotamt,rOrd);
    setprimintrinsic(0,'transform',@elemnum,base_xform,'set');
}
